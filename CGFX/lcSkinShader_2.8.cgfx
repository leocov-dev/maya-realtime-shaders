///////////////////////////////////////////////////////////////////////////////////////
//  lcSkinShader.cgfx
//
//  Author: Leonardo Covarrubias
//  Date: 08/12/2010
//  Contact: leo.covarrubias@live.com
//  Website: blog.leocov.com - www.leocov.com
//
//  Special thanks for inspiration and learing:
//
//  Ben Cloward - http://www.bencloward.com
//  Brice Vandemoortele - http://www.mentalwarp.com
//  Joel Styles - Fake Subsurface Scattering - www.jistyles.com
//  'The Renderman Sading Language Guide' - Rudy Cortes - www.rslprogramming.com - many basic and shading functions
//  RenderMonkey by ATI - http://developer.amd.com/gpu/rendermonkey/Pages/default.aspx
//  Charles Hollemeersch - litsphere shader - http://charles.hollemeersch.net/
//  'Advanced Game Development with Programmable Graphics Hardware' - Alan Watt, Fabio Policarpo - 1D texture mapping
//  Siggraph 2010 'Uncharted 2: Character Lighting and Shading' Presentation by John Hable - sourced from http://advances.realtimerendering.com
//  'Uncharted 2: HDR Lighting' presentation by John Hable
//
//  This work is licensed under a  Creative Commons Attribution-Share Alike 3.0 Unported License
//  http://creativecommons.org/licenses/by-sa/3.0/
//
//  Change List:
//
//  1.0 - 08/21/2010 - Initial Release
//  1.1 - 08/22/2010 - added linear lighting option - sRGB correction for color swatches and color textures
//  2.0 - 09/06/2010 - rewrite - illuminance loop style and simplification of the number and complexity of functions
//  2.1 - 10/26/2010 - bugs - took some things out of the light loop that where misplaced - accidentaly flipflopping suppressCyan
//  2.2 - 12/14/2010 - changed to 2-pass system for backface lighting - avoids faceforward() edge shading errors
//  2.3 - 01/09/2011 - added masks for cubemap reflection and rimlight - thanks to feedback from Richard Smith
//  2.4 - 01/13/2011 - changed light 3 to a point light
//  2.5 -            - skipped
//  2.6 -            - skipped
//  2.7 - 02/24/2011 - lights are now 3 directional and 3 point - switched to gp4fp and gp4vp profiles as they provide support for more math
//  2.8 - 05/23/2011 - wrinkle map with facial regions defined thru vertex color, took out all static shadow map code because it wasnt worth it - thanks to Richard Smith (http://www.richardjohnsmith.co.uk) for suggesting wrinkle maps
//
///////////////////////////////////////////////////////////////////////////////////////

#define MAX_LIGHTS 6

string description = "Leo Covarrubias - blog.leocov.com";
string URL = "http://blog.leocov.com/search/label/cgfx";

//====================================//
////// User Tweakables /////////////////
//====================================//

float globalTonemap
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "Basic Global Tonemapping";
> = 0.0;

float exposure
<
  float UIMin     = -10.0;
  float UIMax     = 10.0;
  float UIStep    = 0.1;
  string UIName   = "Global Exposure";
> = 0.0;

bool linear
<
  string UIName   = "Linear Color Math?";
> = true;

//Ambeint
float3 ambientColor
<
  string UIName   = "Ambient Color";
  string Type     = "Color";
> = {0.0, 0.0, 0.0};

//Diffuse
float3 diffuseColor
<
  string UIName   = "Diffuse Color";
  string Type     = "Color";
> = {0.5, 0.5, 0.5};

float3 specularColor
<
  string UIName   = "Specular Color";
  string Type     = "Color";
> = {0.7, 0.7, 0.7};

float specularPower
<
  float UIMin     = 1.0;
  float UIMax     = 256.0;
  float UIStep    = 0.1;
  string UIName   = "Specular Power";
> = 30.0;

float softenDiffuseA
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "Soften Base Diffuse Shading";
> = 1.0;

float softenDiffuseB
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "SSS Soften Diffuse Shading";
> = 0.1;

float sssAmount
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "SSS Blended Normals Intensity";
> = 0.7;

float suppressCyan
<
  float UIMin      = 0;
  float UIMax      = 1.0;
  float UIStep     = 0.1;
  string UIName    = "SSS Suppress Cyan";
> = 0.97;

bool spacerDiffuse<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Diffuse Textures
bool useDiffuseMap
<
  string UIName   = "Use A Diffuse Texture?";
> = false;

texture diffuseMap
<
  string ResourceType = "2D";
  string UIName       = "Diffuse Map";
>;

sampler2D diffuseMapSampler = sampler_state
{
  Texture       = <diffuseMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  MaxAnisotropy = 4;  //some people may need to comment this out
  LODBias       = -0.5;
};

bool spacerSpec<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Specular Additional Attributes
bool useSpecularMap
<
  string UIName = "Use A Specular Map?";
> = false;

bool useGlossAlpha
<
  string UIName = "Use a Gloss Map? (specular.a)";
> = false;

float specNormalScale
<
  float UIMin = 0.0;
  float UIMax = 2.0;
  float UIStep = 0.1;
  string UIName = "Specular Normals Intensity";
> = 1.0;

float specFresnelExp
<
  float UIMin = 0.0;
  float UIMax = 5.0;
  float UIStep = 0.1;
  string UIName = "Specular Fresnel Exponent";
> = 1.0;

float specFresnelScale
<
  float UIMin = 1.0;
  float UIMax = 15.0;
  float UIStep = 0.1;
  string UIName = "Specular Fresnel Multiplier";
> = 3.0;

texture specularMap
<
  string ResourceType = "2D";
  string UIName       = "Specular Map";
>;

sampler2D specularMapSampler = sampler_state
{
  Texture       = <specularMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  LODBias       = -0.5;
};

bool spacerN<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Normal Map Textures
bool useNormalMap
<
  string UIName   = "Use A Normal Map?";
> = false;

float normalIntensity
<
  float UIMin     = 0.0;
  float UIMax     = 3.0;
  float UIStep    = 0.1;
  string UIName   = "Normal Map Intensity";
> = 1.0;

bool flipGreen
<
  string UIName   = "Invert Normal Map Green Channel?";
> = false;

//Normalmap Sampler
texture normalMap
<
  string ResourceType = "2D";
  string UIName       = "Normal Map";
>;

sampler2D normalMapSampler = sampler_state
{
  Texture       = <normalMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  MaxAnisotropy = 4; //some people may need to comment this out
  LODBias       = -0.5;
};

bool useWrinkleNormalMap
<
  string UIName   = "Use A Wrinkle Normal Map?";
> = false;

texture wrinkleNormalMap
<
  string ResourceType = "2D";
  string UIName       = "Wrinkle Normal Map";
>;

sampler2D wrinkleNormalMapSampler = sampler_state
{
  Texture       = <wrinkleNormalMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  MaxAnisotropy = 4; //some people may need to comment this out
  LODBias       = -0.5;
};

float wmLeftBrow
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.1 - Left Brow    ";
> = 0.0;

float wmMidBrow
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.2 - Middle Brow ";
> = 0.0;

float wmRightBrow
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.3 - Right Brow  ";
> = 0.0;

float wmLeftCheek
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.4 - Left Cheek  ";
> = 0.0;

float wmRightCheek
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.5 - Right Cheek";
> = 0.0;

float wmRightFace
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.6 - Right Face   ";
> = 0.0;

float wmLeftFace
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "0.7 - Left Face     ";
> = 0.0;

bool spacerRim<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Rim Light
bool useRimLight
<
  string UIName   = "Use Rim Light?";
> = false;

bool useRimLightSec
<
  string UIName   = "Use Secondary Rim Light?";
> = false;

float rimExponent
<
  float UIMin     = 0.0;
  float UIMax     = 5.0;
  float UIStep    = 0.1;
  string UIName   = "Rim Light Exponent";
> = 2.2;

float3 rimColor
<
  string UIName   = "Rim Light Color";
  string Type     = "Color";
> = {0.75, 0.9, 1.0};

float3 rimColorSec
<
  string UIName   = "Rim Light B Color";
  string Type     = "Color";
> = {0.75, 0.9, 1.1};

texture rimMask
<
  string ResourceType = "2D";
  string UIName       = "Rim Light Mask Map";
>;

sampler2D rimMaskSampler = sampler_state
{
  Texture   = <rimMask>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapS     = Wrap;
  WrapT     = Wrap;
};

bool spacerHemi<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Hemispherical Ambient Shading
bool useAmbientHemi
<
  string UIName = "Use Hemispherical Ambient?";
> = false;

float3 skyColor
<
  string UIName   = "Hemispherical Sky Color";
  string Type     = "Color";
> = {0.7, 0.95, 1.0};

float3 groundColor
<
  string UIName   = "Hemispherical Ground Color";
  string Type     = "Color";
> = {0.23, 0.27, 0.12};

bool spacerCube<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Cubemap for Reflections and Ambient Shading
bool useAmbCube
<
  string UIName = "Use Cubemap for Ambient?";
> = false;

float ambCubeScale
<
  float UIMin     = 0.0;
  float UIMax     = 20.0;
  float UIStep    = 0.1;
  string UIName   = "Ambient Cubemap Intensity";
> = 1;

bool useReflCube
<
  string UIName = "Use Cubemap for Reflection?";
> = false;

float reflCubeScale
<
  float UIMin     = 0.0;
  float UIMax     = 20.0;
  float UIStep    = 0.1;
  string UIName   = "Reflection Cubemap Intensity";
> = 1;

float reflFresnelExp
<
  float UIMin = 0.0;
  float UIMax = 5;
  float UIStep = 0.1;
  string UIName = "Reflection Fresnel Exponent";
> = 1.0;

float reflCubeGain
<
  float UIMin     = 0.1;
  float UIMax     = 0.9;
  float UIStep    = 0.1;
  string UIName   = "Reflection Cubemap Contrast";
> = 0.5;

float glossBlurWeight
<
  float UIMin = 0.0;
  float UIMax = 1.0;
  float UIStep = 0.1;
  string UIName = "Gloss Blurring Amount";
> = 0.0;

texture envCubeMap
<
  string UIName = "Reflection Env Map";
  string ResourceType = "Cube";
>;

samplerCUBE envCubeMapSampler = sampler_state
{
  Texture = <envCubeMap>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapR = ClampToEdge;
  WrapS = ClampToEdge;
  WrapT = ClampToEdge;
};

texture reflectionMask
<
  string ResourceType = "2D";
  string UIName       = "Cubemap Reflection Mask Map";
>;

sampler2D reflectionMaskSampler = sampler_state
{
  Texture   = <reflectionMask>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapS     = Wrap;
  WrapT     = Wrap;
};

////////////////////////////////////////
//     Lights - position and color    //
////////////////////////////////////////

bool spacerDirLights1<string UIName   = "-------------------------------------";> = false;
bool spacerDirLights2<string UIName   = "Directional Lights>>>>>>>>";> = false;
bool spacerDirLights3<string UIName   = "-------------------------------------";> = false;

float4 light1Dir : DIRECTION
<
  string UIName = "Light 1 - Directional Light";
  string Space  = "World";
> = {0.0, -1.0, -1.0, 0.0};

float3 light1Color
<
  string UIName   = "Light1Color";
  string Type     = "Color";
> = {1.0, 1.0, 1.0};

bool spacerLights2<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light2Dir : DIRECTION
<
  string UIName = "Light 2 - Directional Light";
  string Space  = "World";
> = {-1.0, 1.0, 1.0, 0.0};

float3 light2Color
<
  string UIName   = "Light2Color";
  string Type     = "Color";
> = {0.00, 0.00, 0.00};

bool spacerLights3<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light3Dir : DIRECTION
<
  string UIName = "Light 3 - Directional Light";
  string Space  = "World";
> = {-1.0, 1.0, 1.0, 0.0};

float3 light3Color
<
  string UIName   = "Light3Color";
  string Type     = "Color";
> = {0.00, 0.00, 0.00};

bool spacerPointLights1<string UIName   = "-------------------------------------";> = false;
bool spacerPointLights2<string UIName   = "Point Lights>>>>>>>>>>>>>>";> = false;
bool spacerPointLights3<string UIName   = "-------------------------------------";> = false;

float4 light4Pos : POSITION
<
  string UIName = "Light 4 - Point Light";
  string Object = "PointLight";
  string Space  = "World";
> = {0.0, 0.0, 0.0, 0.0};

float3 light4Color
<
  string UIName   = "Light4Color";
  string Type     = "Color";
> = { 0.00, 0.00, 0.00};

float light4atten
<
  float UIMin = 0.0;
  float UIMax = 10000.0;
  float UIStep = 0.1;
  string UIName = "Light 4 Attenuation";
> = 1.0;

bool spacerLights5<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light5Pos : POSITION
<
  string UIName = "Light 5 - Point Light";
  string Object = "PointLight";
  string Space  = "World";
> = {0.0, 0.0, 0.0, 0.0};

float3 light5Color
<
  string UIName   = "Light5Color";
  string Type     = "Color";
> = { 0.00, 0.00, 0.00};

float light5atten
<
  float UIMin = 0.0;
  float UIMax = 10000.0;
  float UIStep = 0.1;
  string UIName = "Light 5 Attenuation";
> = 1.0;

bool spacerLights6<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light6Pos : POSITION
<
  string UIName = "Light 6 - Point Light";
  string Object = "PointLight";
  string Space  = "World";
> = {0.0, 0.0, 0.0, 0.0};

float3 light6Color
<
  string UIName   = "Light6Color";
  string Type     = "Color";
> = { 0.00, 0.00, 0.00};

float light6atten
<
  float UIMin = 0.0;
  float UIMax = 10000.0;
  float UIStep = 0.1;
  string UIName = "Light 6 Attenuation";
> = 1.0;

////////////////////////////////////////
//     Auto Maxtricies                //
////////////////////////////////////////

float4x4 WorldViewProjection   : WorldViewProjection   ;
float4x4 WorldInverseTranspose : WorldInverseTranspose ;
float4x4 ViewInverse           : ViewInverse           ;
float4x4 World                 : World                 ;

////////////////////////////////////////
//     Fragment Program Functions     //
////////////////////////////////////////

float3 ungamma22 (float3 input)
{
  return (linear) ? pow(input,2.2) : input;
}

float3 gamma22 (float3 input)
{
  return (linear) ? pow(input,1/2.2) : input;
}

float bias (float val, float b) //Rudy Cortes - The Renderman Shading Language Guide
{
  return (b>0) ? pow(abs(val),log(b) / log(0.5)) : 0;
}

float gain (float val, float g) //Rudy Cortes - The Renderman Shading Language Guide
{
  return 0.5 * ((val<0.5) ? bias(2.0*val, 1.0-g) : (2.0 - bias(2.0-2.0*val, 1.0-g)));
}

float3 filmicTonemap(float3 input)
{
  float A = 0.22;
  float B = 0.3;
  float C = 0.1;
  float D = 0.2;
  float E = 0.01;
  float F = 0.3;
  float linearWhite = 11.2;
  float3 Fcolor = ((input*(A*input+C*B)+D*E)/(input*(A*input+B)+D*F)) - E/F;
  float  Fwhite = ((linearWhite*(A*linearWhite+C*B)+D*E)/(linearWhite*(A*linearWhite+B)+D*F)) - E/F;
  return Fcolor/Fwhite;
}

float attenuation (float3 L, float scale)
{
    float d = length(L);
    return (1/d)*scale;
}

float3 blinn (float3 L, float3 V, float3 N, float roughness)
{
  float3 H = normalize(L + V);
  float NdH = max(dot(N,H),0.f);
  return smoothstep(-0.1,0.1,dot(N,L)) * pow(NdH, roughness);
}

float lambertSkinComp (float3 L, float3 N)
{
  return smoothstep(-softenDiffuseB,1, lerp(dot(N,L),dot(N,L)*0.85+0.15,softenDiffuseA ) );//smoothstep lets you pull the diffuse shading edge back to soften the look
}

float3 skinShading (float3 L, float3 Nw, float3 Nn)
{
//Blended Normals SSS Hack  - from John Hable's Siggraph 2010 presentation
  //This sets the red vs green/blue edge bleed
  float Rl  = lerp(0,0.7,sssAmount);
  float GBl = lerp(0,0.1,sssAmount);
  // Diffuse between geometry and map normal
  float DNw = lambertSkinComp(L, Nw);
  float DNn = lambertSkinComp(L, Nn);
  float3 blendedDiff = float3( lerp(DNn,DNw,Rl),lerp(DNn,DNw,GBl),lerp(DNn,DNw,GBl) ); //combine blended diffuse components
  blendedDiff.b = min(blendedDiff.r+(1-suppressCyan), blendedDiff.b);//surpresses blue, invert suppressCyan for better UI usability
  blendedDiff.g = min(blendedDiff.r+(1-suppressCyan), blendedDiff.g);//surpresses green, invert suppressCyan for better UI usability

  return float3( blendedDiff );
}

float fresnel (float3 N, float3 V, float fresnelExp) //this is the most basic approximation of the fresnel function
{
    return max(pow(abs(1.0-dot(N,V)),fresnelExp),0.f);
}

//this returns a float for mipmap level selection
float glossBlur (float specPow, float glossTex, float weight)
{
  float mipMax = 7;
  specPow = (specPow>=1) ? 1 : specPow;
  float G = (specPow*glossTex);
  return mipMax*G*weight;
}

//////////////////////////////////////////////////
////// Compartmentalized Shading Components //////
//////////////////////////////////////////////////

////////////////////////////////////////
// Tangent Space Normal Mapping
float3 normalsTangent (float3 normalTexture,
                       float scale,
                       float3 Nn,
                       float3 Bn,
                       float3 Tn,
                       float3 V)
{
  if(flipGreen)normalTexture.g = 1.0 - normalTexture.g;

//  if(backFaceLighting == true) Nn = faceforward(Nn,-V,Nn);
  if(useNormalMap)
  {
    normalTexture.rgb = normalTexture.rgb * 2.0 - 1.0;
    normalTexture.xy *= scale;

    if (scale > 0) Nn = normalize((normalTexture.x*Tn)+(normalTexture.y*Bn)+(normalTexture.z*Nn));
  }

  return Nn;
}

////////////////////////////////////////
// Ambient Shading - Hemisphere, and Cubemap
float3 ambientEnv (float3 N)
{
  groundColor = ungamma22(groundColor);
  skyColor = ungamma22(skyColor);

  if(useAmbientHemi) return lerp(groundColor,skyColor,max(0.5*N.y+0.5,0.f));  //linearly interpolate between the two colors based on the World Normal's Y/up vector (shifted into 0-1 range)
  if(useAmbCube) return ungamma22(texCUBElod(envCubeMapSampler, float4(N,7.8)).rgb) * ambCubeScale;
  return 1;
}

////////////////////////////////////////
// Rim lighting effect
float3 rimLightDual (float3 diffuseComp,
                    float3 V,
                    float3 Nn)
{
  rimColor = ungamma22(rimColor);
  rimColorSec = ungamma22(rimColorSec);
  float Fr = fresnel(Nn,V,rimExponent);
  float3 Rim = 0;
  if(useRimLight)    Rim += Fr*rimColor*max(pow(dot(Nn,float3(0,1,0))*0.5+.5,2),0.f);//I use a half lambert style calculation on N.Y instead of standard lambert because it gives a softer blend across the surface
  if(useRimLightSec) Rim += Fr*rimColorSec*(1.0-diffuseComp);
  return Rim;
}

////////////////////////////////////////
// CubeMap Reflection
float3 reflectionCube (float4 specularTexture,
                       float3 N,
                       float3 V)
{
  float3 REnv = 0;
  float3 R = reflect(-V,N); //reflection vector
  float roughness = (useGlossAlpha) ? specularTexture.a : 1;
  float Fr = fresnel(N,V,reflFresnelExp);
  float miplvl = glossBlur (specularPower, roughness, glossBlurWeight); //set a mipmap level to give the impression of blur
  if(useReflCube) REnv = ungamma22(texCUBElod(envCubeMapSampler, float4(R,miplvl)).rgb);  //lets you choose a mip level for the cube map to make it appear blurred

  //apply a contrast function to increase or decrease contrast - most useful as an increase
  REnv.r = gain(REnv.r,reflCubeGain);
  REnv.g = gain(REnv.g,reflCubeGain);
  REnv.b = gain(REnv.b,reflCubeGain);

  return REnv*reflCubeScale*Fr;
}

////////////////////////////////////////
//     Structs                        //
////////////////////////////////////////

// input from application
struct app2vert {
  float4 Position : POSITION;
  float2 TexCoord : TEXCOORD0;
  float2 TexCoord2: TEXCOORD1;
  float4 Normal   : NORMAL;
  float4 Binormal : BINORMAL;
  float4 Tangent  : TANGENT;
  float4 VertColor: COLOR0;
};

// output to fragment program
struct vert2pixel {
  float4 hpos          : POSITION;
  float2 UV            : TEXCOORD0;
  float3 worldNormal   : TEXCOORD2;
  float3 worldBinormal : TEXCOORD3;
  float3 worldTangent  : TEXCOORD4;
  float3 eyeVec        : TEXCOORD5;
  float3 vertColor     : TEXCOORD6;
  float3 WSPos         : TEXCOORD7;

};

////////////////////////////////////////
//     VERTEX SHADER                  //
////////////////////////////////////////

vert2pixel VS(app2vert IN, uniform bool backface)
{
  vert2pixel OUT;

  OUT.vertColor = IN.VertColor.rgb;

  OUT.hpos = mul(WorldViewProjection, IN.Position);
  OUT.UV = IN.TexCoord.xy;

  OUT.worldNormal   = (backface) ? -1*mul(WorldInverseTranspose, IN.Normal).xyz : mul(WorldInverseTranspose, IN.Normal).xyz;
  OUT.worldTangent  = mul(WorldInverseTranspose, IN.Tangent).xyz;
  OUT.worldBinormal = mul(WorldInverseTranspose, IN.Binormal).xyz;

  OUT.WSPos = mul(World, IN.Position).xyz;
  OUT.eyeVec.xyz = float3(ViewInverse[0].w,ViewInverse[1].w,ViewInverse[2].w) - OUT.WSPos.xyz;

  return OUT;
}

////////////////////////////////////////
//     Pixel Shaders                  //
////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////
//     Normals Only
//////////////////////////////////////////////////////////////////////////////////////////////
float4 PSNormals (vert2pixel IN) : COLOR //visualize the normals in worldspace
{
  float2 UV = IN.UV;
  //////////////////////
  // Normal Component //
  float3 N = normalsTangent(tex2D(normalMapSampler, UV),
                            normalIntensity,
                            normalize(IN.worldNormal),
                            normalize(IN.worldBinormal),
                            normalize(IN.worldTangent),
                            normalize(IN.eyeVec.xyz));
  N = 0.5 * N + 0.5;  //I need to shift the normals into the 0-1 range for viewing as color
                      //otherwise they are in the -1 to 1 range
  /////////////////////
  // Alpha Component //
  float Alpha = 1;

  return float4(N,Alpha);
}

//////////////////////////////////////////////////////////////////////////////////////////////
//     Full Shading
//////////////////////////////////////////////////////////////////////////////////////////////

float4 PSFull (vert2pixel IN): COLOR
{
  float3 lights [MAX_LIGHTS][3]= { { (-light1Dir.xyz),       light1Color, 0.0 },
                                   { (-light2Dir.xyz),       light2Color, 0.0 },
                                   { (-light3Dir.xyz),       light3Color, 0.0 },
                                   { (light4Pos - IN.WSPos), light4Color, light4atten },
                                   { (light5Pos - IN.WSPos), light5Color, light5atten },
                                   { (light6Pos - IN.WSPos), light6Color, light6atten }
                                 };

  float3 V   = normalize(IN.eyeVec);
  float3 Nw  = normalize(IN.worldNormal);
  float3 Bw  = normalize(IN.worldBinormal);
  float3 Tw  = normalize(IN.worldTangent);

  float2 UV   = IN.UV;
  
  float3 vertexColors = IN.vertColor;

  // Texture Components
  float3 Nt   = tex2D(normalMapSampler, UV).rgb;
  float3 Nwkl = tex2D(wrinkleNormalMapSampler, UV).rgb;
  float4 Dt   = tex2D(diffuseMapSampler, UV);
  Dt.rgb = ungamma22(Dt.rgb);
  float4 St   = tex2D(specularMapSampler, UV);
  St.rgb = ungamma22(St.rgb);
  float3 Rimt = tex2D(rimMaskSampler, UV).rgb;
  float3 Reflt = tex2D(reflectionMaskSampler, UV).rgb;

  ambientColor  = ungamma22(ambientColor);
  diffuseColor  = ungamma22(diffuseColor);
  specularColor = ungamma22(specularColor);

  ///////////////////////
  // Normal Component //
  float3 NormalCombi = Nt;
  if(useWrinkleNormalMap){
    NormalCombi = lerp(Nt         , Nwkl, (-10*abs(clamp(vertexColors,0.0,0.2)-0.1)+1)*wmLeftBrow   );
    NormalCombi = lerp(NormalCombi, Nwkl, (-10*abs(clamp(vertexColors,0.1,0.3)-0.2)+1)*wmMidBrow    );
    NormalCombi = lerp(NormalCombi, Nwkl, (-10*abs(clamp(vertexColors,0.2,0.4)-0.3)+1)*wmRightBrow  );
    NormalCombi = lerp(NormalCombi, Nwkl, (-10*abs(clamp(vertexColors,0.3,0.5)-0.4)+1)*wmLeftCheek  );
    NormalCombi = lerp(NormalCombi, Nwkl, (-10*abs(clamp(vertexColors,0.4,0.6)-0.5)+1)*wmRightCheek );
    NormalCombi = lerp(NormalCombi, Nwkl, (-10*abs(clamp(vertexColors,0.5,0.7)-0.6)+1)*wmRightFace  );
    NormalCombi = lerp(NormalCombi, Nwkl, (-10*abs(clamp(vertexColors,0.6,0.8)-0.7)+1)*wmLeftFace   );
  }
  
  float3 N = normalsTangent(NormalCombi,
                            normalIntensity,
                            Nw,
                            Bw,
                            Tw,
                            V);
  float3 Nspec = normalsTangent(NormalCombi,
                                normalIntensity*specNormalScale,
                                Nw,
                                Bw,
                                Tw,
                                V);



  // Light Loop
  // Diffuse and Specular
  float3 Ci = 0;  //Incident Color
  float3 Cd = 0;  //Diffuse of lights only, no textures

  float FrSpec = fresnel(N,V,specFresnelExp)*specFresnelScale;

  if(useGlossAlpha) specularPower*=pow((St.a+1),2);  //here i take the aproach of increasing the specularPower by the alpha instead of simply multiplying and reducing specPower - this avoids issues when specPower aproaches Zero - using pow() is arbitrary but creates a visualy pleasing distinction between sizes
  if(useSpecularMap) specularColor*=St.rgb;
  if(specularPower<1)specularColor=0; //avoids problems with small specularPower values

  for ( int i = 0; i < MAX_LIGHTS; i++  ) //Illuminance loop
  {
    float3 L = normalize(lights[i][0]);
    float3 Lc = ungamma22(lights[i][1]); //light color is an interface color swatch and needs to be gamma corrected
    if(i>2) Lc*=attenuation(lights[i][0], lights[i][2]);

    float3 Diffuse  = skinShading(L,Nw,N);

    float3 Spec1 = blinn(L, V, Nspec, specularPower); //primary specular
    float3 Spec2 = blinn(L, V, Nspec, specularPower*10); //secondary specular slightly bigger

    Diffuse *= Lc;
    Cd += Diffuse; //store a variable of just the diffuse light intensity/colors
    Diffuse *= (useDiffuseMap) ? Dt.rgb : diffuseColor;

    float3 Specular = (Spec1+Spec2)*Lc* FrSpec * specularColor;

    Ci += (Diffuse+Specular);
  }
  // Rim Light Component
  Ci += rimLightDual (Cd,  //use Cd instead of Ci to mask the rim light by only the diffuse light intensity
                      V,
                      N) * Rimt;

  // Cubemap Reflection Component
  Ci += reflectionCube(St,
                       N,
                       V)* Reflt;

  //Ambient Component
  ambientColor *= ambientEnv(N);
  Ci += (useDiffuseMap) ? ambientColor*Dt.rgb : ambientColor*diffuseColor;

  // Alpha Component
  float Oi = 1;

  Ci = lerp(gamma22(Ci*pow(2,exposure)),gamma22(filmicTonemap(Ci*pow(2,exposure))),globalTonemap);

  return float4(Ci,Oi);  //Incident Color and Incident Opacity
}

////////////////////////////////////////
//     Techniques                     //
////////////////////////////////////////

technique Blended_Normals_Skin_Shading
{
  pass P0
  {
    CullFaceEnable=true;
    CullMode = Back;
    VertexShader = compile gp4vp VS(false);
    PixelShader  = compile gp4fp PSFull();
  }
}

technique Blended_Normals_Skin_Shading_2_Sided
{
  pass Back
  {
    CullFaceEnable=true;
    CullMode = Front;
    VertexShader = compile gp4vp VS(true);
    PixelShader  = compile gp4fp PSFull();
  }
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    VertexShader = compile gp4vp VS(false);
    PixelShader  = compile gp4fp PSFull();
  }
}

technique World_Normals
{
  pass Back
  {
    CullFaceEnable=true;
    CullMode = Front;
    VertexProgram   = compile gp4vp VS(true);
    FragmentProgram = compile gp4fp PSNormals();
  }
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    VertexProgram   = compile gp4vp VS(false);
    FragmentProgram = compile gp4fp PSNormals();
  }
}

technique _ //empty technique for Maya 2011
{
  pass P0
  {
    //empty
  }
}